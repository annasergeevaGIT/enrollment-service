plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
}

group = 'at'
version = '0.0.1-SNAPSHOT'
description = 'e-learning-platform'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	sourceCompatibility = '21'
}

jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url = "https://packages.confluent.io/maven/"
	}
}

ext {
	jpaModelGenVersion = '6.5.0.Final' // generates metadata about JPA entities for typesafe criteria queries ./gradlew assemble -> Course_
	hypersistenceVersion = '3.7.5'  // JSONB support, utilities for performance optimization and query building
    mapstructVersion = '1.5.5.Final'
    mapstructLombokBindingVersion = '0.2.0'
	wiremockVersion = '3.2.0'
	springdocVersion = '2.5.0' //swagger documentation
	springCloudVersion = '2025.0.0' //config server
	avroSerializerVersion = '7.6.0'
	avroVersion = '1.11.3'
	awaitilityVersion = '4.2.1'
	lokiAppenderVersion = '2.0.1'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer' // for load balancing Eureka + gateway
	implementation 'org.springframework.boot:spring-boot-starter-webflux' // for load balancing Eureka + gateway
    implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation "io.hypersistence:hypersistence-utils-hibernate-63:${hypersistenceVersion}"
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
	implementation 'org.springframework.cloud:spring-cloud-starter-config' //config server
	implementation 'org.springframework.retry:spring-retry' //config server
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation "io.confluent:kafka-avro-serializer:${avroSerializerVersion}"
	implementation "org.apache.avro:avro:${avroVersion}"
	implementation 'org.apache.httpcomponents.client5:httpclient5' // better for vt
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly "com.github.loki4j:loki-logback-appender:${lokiAppenderVersion}"
	annotationProcessor "org.hibernate:hibernate-jpamodelgen:${jpaModelGenVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${mapstructLombokBindingVersion}"
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'com.squareup.okhttp3:mockwebserver' //HTTP server for mocking external API calls during tests
	testImplementation "org.wiremock:wiremock-standalone:${wiremockVersion}"
	testImplementation 'org.testcontainers:kafka'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation "org.awaitility:awaitility:${awaitilityVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

bootBuildImage {
	imageName = "${project.name}"
	environment = ["BP_JVM_VERSION": "21.*"]
}

tasks.named('test') {
	useJUnitPlatform()
}
